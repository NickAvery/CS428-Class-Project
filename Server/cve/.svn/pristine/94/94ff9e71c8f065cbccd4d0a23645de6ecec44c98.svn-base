#
# cved.icn - the CVE server daemon
# Early Contributors: Laurel Cutting, Mike Kemp
# Maintainers: Clinton Jeffery, Jafar Al Gharaibeh
#
$include "../common/defaults.icn"

global server, DAT, CONFIG,  USER_LOCALPATH, USER_GLOBALPATH,
       GROUP_GLOBALPATH, PROJECT_GLOBALPATH, NEWSFEED_GLOBALPATH, 
       SESSION_GLOBALPATH

global  WB_PATH, NODERPATH, EDGERPATH
global  FIRST_PORT

global TDays, THours

procedure main(av)
   local cwd, i, fin, p, s, mod, LDays, d, h, npc_mode, npc_n, com

   trap("SIGINT", SIGINT_handler)

   cwd := chdir()
   if cwd[-3:0]=="bin" then chdir("..")
   else if cwd[-10:0] == ("src" || PS || "server") then {
       chdir(".."); chdir("..")
   }
   else write("didn't see where I was to chdir to; staying put in ", cwd)

   DAT := "dat" || PS || "nmsu"
  
   every i:=1 to *av do
      case av[i] of {
	 "-port" | "-p" : p := integer(av[i+1])
	 "-LTnpc" : {
	     com := &main
	     npc_mode := "T"
	     npc_n := integer(av[i+1])
	     }
	 "-Tnpc" : {
	     npc_mode := "T"
	     npc_n := integer(av[i+1])
	     }
	 "-Pnpc" :{
	     npc_mode := "P"
	     npc_n := integer(av[i+1])
            }
	 "-model" | "-m" : {
	    s := av[i+1]
	    if find("idaho",s) then {
               mod := "uidaho"
	       DAT := "dat" || PS || "uidaho"
	       }
	    else if find("nmsu",s) then {
               mod := "nmsu" 
	       DAT := "dat" || PS || "nmsu"
	       }
	    else {
	       mod := s
	       DAT := "dat" || PS || s
	       }
            if fin := open("dat" || PS || "config.cve", "w") then {
               write(fin, "model ", mod)
               close(fin)
	    }
         }
      } # case
   CONFIG := "dat" || PS || "config.cve"
   if fin := open(CONFIG) then {
       DAT := "dat" || PS || (read(fin) ? (="model " & tab(0)))
       close(fin)
   }      
      
   USER_LOCALPATH := DAT ||PS|| "users"
   USER_GLOBALPATH := "dat" ||PS|| "users"
   GROUP_GLOBALPATH := "dat" ||PS|| "groups"
   PROJECT_GLOBALPATH := "dat" ||PS|| "projects"
   NEWSFEED_GLOBALPATH := "dat" ||PS|| "newsfeed"
   SESSION_GLOBALPATH := "dat" ||PS|| "sessions"
   WB_PATH := DAT||PS||"whiteboards"
   NODERPATH := DAT||PS||"nodes"
   EDGERPATH := DAT||PS||"edges"   
   
   if /p & find("nmsu", DAT) then p:=4600
   
   if /p then FIRST_PORT := DEFAULT_PORT
   else FIRST_PORT := integer(p)

   server := Server(p, npc_mode, npc_n, com) 
   LDays := ["Saturday", "Sunday", "Monday", "Tuesday", 
             "Wednesday", "Thursday", "Friday"]
   TDays := table()

   every i:= 1 to *LDays do {
      TDays [LDays[i]] := table()
      }

   every d := key(TDays) do {
      THours := table()
      TDays[d] := THours
      every h := 1 to 24 do {
         THours[h] := 0
         TDays[d][h] := 0
         }
      }

   server.open_TCP_listener()
   #server.open_UDP_listener()
   server.run()
end 

#
# SIGINT_handler: handles CTRL-Cs to this process
#
procedure SIGINT_handler(s)
   server.shutdown()
end
   
