#
# delta.icn - perform translation on .dat files
#
# Use when a .dat file was done with coordinates at the wrong base x/y/z
# location.  ASSUMES all x,y,z values are given on a single line, and
# that only translatable things will consist of three values.  At present,
# MISSES virtual objects with x,y,z given on three separate fieldnames,
# and CORRUPTS .dat files that have non-xyz numeric data with 3+ values
# on a line.
#
procedure main(av)
(*av = 3) | stop("usage: delta dx dy dz <infile >outfile")
fin := &input
fout := &output
dx := numeric(av[1]) | stop("usage: delta dx dy dz <infile >outfile")
dy := numeric(av[2]) | stop("usage: delta dx dy dz <infile >outfile")
dz := numeric(av[3]) | stop("usage: delta dx dy dz <infile >outfile")
while line := read(fin) do {
   line ? {
      if (pre := tab(upto(&digits))) &
	 (num1 := tab(many(&digits++'.'))) &
	 (mid1 := tab(many(' \t,'))) &
	 (num2 := tab(many(&digits++'.'))) &
	 (mid2 := tab(many(' \t,'))) &
	 (num3 := tab(many(&digits++'.'))) &
	 (post := tab(0)) then {
	    write(fout,pre,num1+dx,mid1,num2+dy,mid2,num3+dz,post)
	 }
      else
	 write(fout, line)

   }
}
end
